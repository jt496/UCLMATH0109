import Mathlib.Tactic

/-

A `Dedekind cut` is a partition of the rationals `(A , Aá¶œ)` such that both A and Aá¶œ are
nonempty with the following properties:

1)  A is a down-set: if x < y and y âˆˆ A then x âˆˆ A;

2)  A has no maximum element: if x âˆˆ A there exists y âˆˆ A with x < y.

One way of constructing the real numbers from â„š is as Dedekind cuts.

We identify a real number `r` with the cut `(A, Aá¶œ)` where `A = {x âˆˆ â„š | x < r}`

-/
/--
Dedekind cut
-/
@[ext] -- two Dedekind cuts D = E iff D.A = E.A
structure Dedekind where
  A         : Set â„š
  nonempty  : A.Nonempty
  nonempty' : Aá¶œ.Nonempty
  down      : âˆ€ â¦ƒx yâ¦„, x < y â†’ y âˆˆ A â†’ x âˆˆ A
  no_max    : âˆ€ â¦ƒxâ¦„, x âˆˆ A â†’ âˆƒ y âˆˆ A, x < y

-- the @[ext] label produces the following two results for free:
#check Dedekind.ext
#check Dedekind.ext_iff

namespace Dedekind
notation "ğ”»" => Dedekind

/- All our results will now be in the Dedekind namespace -/
/-
We open a named section to explain what we are trying to prove (we will prove
some basic results in this section hence the name).
-/
section basics
variable {D : ğ”»}
variable {x y : â„š}
/-
The use of a section allows us to introduce variables into the local context
 that will vanish once the section ends.
-/
/-- If `D = (A , Aá¶œ)` is a Dedekind cut with `x âˆˆ A` and `y âˆˆ Aá¶œ` then `x < y`-/
lemma lt_of_cut (hx : x âˆˆ D.A) (hy : y âˆˆ D.Aá¶œ) : x < y :=by
  by_contra! h
  cases h.lt_or_eq with
  | inl h =>  exact hy <| D.down h hx
  | inr h =>  exact hy (h â–¸ hx)



#check D.A         -- D.A : Set â„š
#check D.nonempty  -- D.nonempty : Set.Nonempty D.A
#check D.nonempty' -- D.nonempty' : Set.Nonempty D.Aá¶œ
#check D.down      -- D.down : âˆ€ â¦ƒx y : â„šâ¦„, x < y â†’ y âˆˆ D.A â†’ x âˆˆ D.A
#check D.no_max    -- D.no_max : âˆ€ â¦ƒx : â„šâ¦„, x âˆˆ D.A â†’ âˆƒ y, y âˆˆ D.A âˆ§ x < y


/-- We can order Dedekind cuts with `D â‰¤ E` iff `D.A âŠ† E.A` -/
instance : LE ğ”» where
  le := fun D E => D.A âŠ† E.A

lemma le' : D â‰¤ E â†” D.A âŠ† E.A :=by rfl

/-- We can define `<` on Dedekind cuts by `D < E` iff `D â‰¤ E` and `D â‰  E`-/
instance : LT ğ”» where
  lt := fun D E => D â‰¤ E âˆ§ Â¬ E â‰¤ D

/-- `D < E` iff `D.A` is a proper-subset of `E.A` -/
lemma lt' : D < E â†” D.A âŠ‚ E.A :=by rfl

/-- D < E iff âˆƒ x âˆˆ E.A \ D.A -/
lemma lt_iff_exists : D < E â†” âˆƒ x, x âˆˆ E.A \ D.A:=by
  rw [lt']
  constructor
  Â· intro hde
    exact Set.exists_of_ssubset hde
  Â· intro âŸ¨x,hxâŸ©
    constructor
    Â· intro y hy
      apply E.down (D.lt_of_cut hy hx.2) hx.1
    Â· intro hf
      apply hx.2 <| hf hx.1

end basics

/-
We now establish that Dedekind cuts form a `Preorder`.

class Preorder (Î± : Type u) extends LE Î±, LT Î± where
  le_refl : âˆ€ a : Î±, a â‰¤ a
  le_trans : âˆ€ a b c : Î±, a â‰¤ b â†’ b â‰¤ c â†’ a â‰¤ c
  lt := fun a b => a â‰¤ b âˆ§ Â¬b â‰¤ a
  lt_iff_le_not_le : âˆ€ a b : Î±, a < b â†” a â‰¤ b âˆ§ Â¬b â‰¤ a := by intros; rfl

-/
instance : Preorder ğ”» where
  le_refl :=by
    intro D; rw [le'];
  le_trans :=by
    intro D E F hde hef
    rw [le'] at *
    apply hde.trans hef

/--
ğ”» contains a copy of the rational numbers given by the embedding `rat`
-/
def rat (q : â„š) : ğ”» :=by
  use {x : â„š | x < q}
  Â· use q - 1
    simp only [Set.mem_setOf_eq, sub_lt_self_iff, zero_lt_one]
  Â· use q; apply lt_irrefl q
  Â· intro x y hxy hyx
    apply hxy.trans  hyx
  Â· intro x hx
    use (q + x) / 2
    simp only [Set.mem_setOf_eq] at *
    constructor <;> linarith

@[simp]
lemma rat' (q : â„š) : (rat q).A = {x : â„š | x < q} := rfl


/-- The map `rat` is an order embedding: i.e. it is injective and `rat p â‰¤ rat q â†” p â‰¤ q`-/
def Rat : â„š â†ªo ğ”» where
  toFun := rat
  inj' :=by
    intro p q hpq
    rw [Dedekind.ext_iff] at hpq
    simp only [rat'] at *
    cases lt_trichotomy p q with
    | inl h =>
      have hq : p âˆ‰ {x | x < p} := lt_irrefl p
      rw [hpq] at hq
      contradiction
    | inr h =>
      cases h with
      | inl h => exact h
      | inr h =>
        have hp : q âˆ‰ {x | x < q} := lt_irrefl q
        rw [â† hpq] at hp
        contradiction
  map_rel_iff' :=by
    intro p q
    -- At this point the goal looks rather unreadable so we try `dsimp`
    dsimp
    -- You could now use constructor followed by ext to finish the proof but
    -- the following result from Mathlib is easier.
    exact forall_lt_iff_le

instance : Zero ğ”» where
zero := rat 0

/--
There is a Dedekind cut corresponding to âˆšn for each n (we already have these for 0, 1 so we start from 2)
-/
def root_n_add_two (n : â„•) : ğ”» :=by
  use { x : â„š | x^2 < n + 2 âˆ¨ x < 0}
  Â· use 0; left; norm_cast; norm_num
  Â· use (n + 2)
    rintro (hf | hf)
    Â· norm_cast at hf
      nlinarith
    Â· norm_cast at hf
  Â· intro x y hlt hy
    simp only [Set.mem_setOf_eq] at *
    by_cases hx : x < 0
    Â· right; exact hx
    Â· cases hy with
      | inl h1 => left; push_neg at hx; apply lt_trans (pow_lt_pow_left hlt hx (by decide)) h1
      | inr h2 => right; exact hlt.trans h2
  Â· intro x hx
    simp only [Set.mem_setOf_eq] at *
    have hn12 : (1 : â„š) < n + 2 := by
      rw [â† Nat.cast_two, â†Nat.cast_one, â† Nat.cast_add, Nat.cast_lt];
      exact Nat.AtLeastTwo.one_lt
    have hn2 : (0 : â„š) < n + 2 := lt_trans rfl hn12
    by_cases hxneg : x < 0
    Â· exact  âŸ¨0, Or.inl (by rw [zero_pow (by decide)]; exact hn2), hxnegâŸ©
    Â· cases hx with
      | inl hsq =>
      -- The next line makes our proof easier to read, but we will need to use `dsimp [e]`
      -- at various points to make sure Lean understands how `e` is defined.
      -- Before starting we establish lots of basic facts
        let e := 1 - (x ^ 2 / (n + 2))
        use x + (e / (2 * x + 1))
        push_neg at hxneg
        have hx20 : 0 â‰¤ x ^ 2 := by exact sq_nonneg x -- exact?
        have h2x0 : 0 < 2 * x + 1 := add_pos_of_nonneg_of_pos (mul_nonneg rfl hxneg) rfl -- apply? and exact?
        have h2n : x ^ 2 / (n + 2) < 1 := (div_lt_one hn2).mpr hsq -- exact?
        have he0 : 0 < e :=by dsimp [e]; exact sub_pos.mpr h2n; -- exact?
        have hx20n : 0 â‰¤ x ^ 2 /(n + 2) := div_nonneg hx20 hn2.le -- exact?
        have he1 : e â‰¤ 1 := by dsimp [e]; exact sub_le_self 1 hx20n -- exact?
        have h2x1 : e â‰¤ 2 * x + 1 := le_add_of_nonneg_of_le (mul_nonneg (by rfl) hxneg) he1 --apply? and exact?
        have hposdiv : 0 < e / (2 * x + 1) := div_pos he0 h2x0 -- exact?
        have hdivle1 : e / (2 * x + 1) â‰¤ 1:= (div_le_one h2x0).mpr h2x1 -- exact?
        have hsqle : (e / (2 * x + 1)) ^ 2 â‰¤ e / (2 * x + 1) := sq_le hposdiv.le hdivle1 -- exact?
        have helt : e < (n + 2) - x ^ 2 := by
          dsimp [e]
          field_simp
          exact div_lt_self (sub_pos.mpr hsq) hn12 -- apply?
        constructor
        Â· left
          calc
            _ = x ^ 2 + 2 * x * e / (2 * x + 1) + (e / (2 * x + 1)) ^ 2 := by rw [add_sq, â† mul_div_assoc] -- expand the brackets
            _ â‰¤ x ^ 2 + 2 * x * e / (2 * x + 1) + e / (2 * x + 1)       := Rat.add_le_add_left.mpr hsqle -- exact?
            _ = x ^ 2 + e / (2 * x + 1) * (2 * x + 1)                   := by rw [mul_div_assoc, add_assoc, â† add_one_mul , mul_comm] -- factorise
            _ = x ^ 2 + e                                               := by rw [div_mul_cancelâ‚€]; exact Ne.symm h2x0.ne
            _ <  x ^ 2 + ((n + 2) - x ^ 2)                              := add_lt_add_left helt (x ^ 2) -- exact?
            _ = _                                                       := add_sub_cancel _ _ -- exact?
        Â· exact lt_add_of_pos_right x hposdiv
      | inr hneg => contradiction

-- The next line prints out our proof of "no_max" from the previous theorem.
-- You can try running this in your solutions to compare
#print root_n_add_two.proof_4 -- 88 lines (see InfoView)



/-
If `S : Set ğ”»` is nonempty and bounded above then it has a supremum defined
by taking the union of cuts in S.
-/

noncomputable
instance : SupSet ğ”» where
  sSup :=by
    intro S
    by_cases h : S.Nonempty âˆ§ BddAbove S
    Â· use  â‹ƒ d âˆˆ S, d.A
      Â· -- the union is nonempty because S is nonempty and each cut in S is nonempty
        obtain âŸ¨d,hdâŸ©:= h.1
        obtain âŸ¨x,hxâŸ© := d.nonempty
        use x
        simp only [Set.mem_iUnion, exists_prop]
        use d,hd,hx
      Â· -- the complement is nonempty uses the fact that there is an upper bound
        -- u for S and the complement of u is nonempty
        obtain âŸ¨u,huâŸ© := h.2
        obtain âŸ¨x,hxâŸ© := u.nonempty'
        use x
        intro hf
        apply hx
        simp only [Set.mem_iUnion, exists_prop] at hf
        obtain âŸ¨i,hi1,hi2âŸ©:= hf
        exact (hu hi1) hi2
      Â· -- if x < y and y is in the union then y is in one of the cuts from S and x is
        -- in the same cut
        intro x y hxy hy
        simp only [Set.mem_iUnion, exists_prop] at *
        obtain âŸ¨i,hi1,hi2âŸ©:= hy
        use i,hi1
        apply i.down hxy hi2
      Â· -- if x is in the union then it is in one of the cuts from S and this cut contains
        -- a larger element which also belongs to the union
        intro x hx
        simp only [Set.mem_iUnion, exists_prop] at *
        obtain âŸ¨i,hi,hixâŸ©:= hx
        obtain âŸ¨y,hyâŸ© := i.no_max hix
        use y, âŸ¨i,hi,hy.1âŸ©, hy.2
-- If S is âˆ… or not bounded above we still need to return something sensible
    Â· exact 0

#check dif_pos -- These can be used to simplify a function defined using `by_cases` or `if then else`
#check dif_neg
-- 1 mark
lemma sSup' (S : Set ğ”») (h1 : S.Nonempty) (h2: BddAbove S) : (sSup S).A = (â‹ƒ d âˆˆ S, d.A) :=by
  dsimp [sSup]
  rw [dif_pos âŸ¨h1,h2âŸ©]

#check IsLeast
#check upperBounds
#check IsLUB

/--
`ğ”»` is complete: any nonempty set of Dedekinds cuts that is bounded above
  has a least upper bound-/
-- 3 marks
theorem complete_lub (S : Set ğ”») (hne: S.Nonempty) (hupb: BddAbove S) :
  IsLUB S (sSup S) :=by
  constructor <;> (intro d hd; rw [le',sSup' S hne hupb])
  Â· exact Set.subset_biUnion_of_mem hd
  Â· exact Set.iUnionâ‚‚_subset_iff.mpr hd
